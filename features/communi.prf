######################################################################
# Communi
######################################################################

include(communi-config.prf)

isEmpty(COMMUNI_INSTALL_LIBS):COMMUNI_INSTALL_LIBS = $$[QT_INSTALL_LIBS]
isEmpty(COMMUNI_INSTALL_BINS):COMMUNI_INSTALL_BINS = $$[QT_INSTALL_BINS]
isEmpty(COMMUNI_INSTALL_HEADERS):COMMUNI_INSTALL_HEADERS = $$[QT_INSTALL_HEADERS]/Communi

macx:!qt_no_framework {
    INCLUDEPATH += $$COMMUNI_INSTALL_LIBS/Communi.framework/Headers
    QMAKE_FRAMEWORKPATH += $$COMMUNI_INSTALL_LIBS
    LIBS += -framework Communi
} else {
    INCLUDEPATH += $$COMMUNI_INSTALL_HEADERS
    DEPENDPATH += $$COMMUNI_INSTALL_HEADERS
    REAL_TEMPLATE = $$TEMPLATE
    TEMPLATE = fakelib
    LIBS += -L$$COMMUNI_INSTALL_LIBS -l$$qtLibraryTarget(Communi)
    TEMPLATE = $$REAL_TEMPLATE
}
